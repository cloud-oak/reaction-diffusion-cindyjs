<!DOCTYPE html>
<html>
	<head>
		<meta http-equiv="content-type" content="text/html; charset=UTF-8">
		<script type="text/javascript" src="https://cindyjs.org/dist/snapshot/Cindy.js"></script>
		<script type="text/javascript" src="https://cindyjs.org/dist/snapshot/CindyGL.js"></script>
		<title>3dgol</title>
	</head>

	<body style="background:#000;">
		<div id="CSCanvas" class="CindyJS-widget" style="position: relative; width: 512px; height: 512px;"></div>
		<script id="csdraw" type="text/x-cindyscript">
			canvas((0, 0), (statewidth, 0), "state",
				x0 = EXTENT * mod(t, TILESX);
				y0 = EXTENT * floor(t / TILESX);
				drawimage((x0, y0), (x0 + EXTENT, y0), "current")
			);
			colorplot((-.5, -.5), (EXTENT-.5, -.5), "current", newstate(#.x, #.y));
			//colorplot((-.5, -.5), (EXTENT-.5, -.5), "current", [get(#.x, #.y), countneighbors(#.x, #.y), 0]);

			// Ab hier: 3D
			target = [EXTENT,EXTENT,EXTENT]/2;
			origin = zoom * EXTENT * [
				cos(lambda)*cos(phi),
				sin(phi),
				sin(lambda)*cos(phi)
			] + target;
			mdir = (target-origin)/|(target-origin)|;
			v = cross([0,1,0], mdir);
			w = cross(mdir, v);
			v = v/|v|;
			w = w/|w|;
			colorplot(
				dir = mdir + #.x*v + #.y*w;
				raytrace()
			);

			drawimage((-1, .5), (-.5, .5), "current", interpolate->false);

			t = mod(t + 1, EXTENT);
		</script>
		<script id="csinit" type="text/x-cindyscript">
			EXTENT = 32; // Extent of the cube for each dimension
			statewidth   = EXTENT * EXTENT;
			stateheight  = EXTENT;
			while(statewidth > 2 * stateheight,
				statewidth  = statewidth / 2;
				stateheight = stateheight * 2;
			);
			TILESX = statewidth / EXTENT;
			TILESY = stateheight / EXTENT;
			print(statewidth + " x " + stateheight);
			lambda = pi / 6;
			phi = pi / 8;
			zoom = 1.1;
			createimage("state", statewidth, stateheight);
			createimage("current", EXTENT, EXTENT);
			get(tx, ty) := imagergb((-.5, -.5), (EXTENT-.5, -.5), "current", (tx, ty), interpolate->false, repeat->false).r;
			voxelAt(p) := (
				if(p.x<0 % p.y < 0 % p.z < 0 % p.x >= EXTENT % p.y >= EXTENT % p.z >= EXTENT,
					0
				,
					p.y = mod(EXTENT - floor(p.y) + t, EXTENT);
					imagergb((0, 0), (statewidth, 0), "state",
						(p.x + EXTENT * mod(p.y, TILESX), p.z + EXTENT * floor(p.y / TILESX)),
						interpolate->false, repeat->false
					).r;
				);
			);
			colorplot("current", if(random() > 0.6, 1, 0)); //random stuff as starting image
			t = 0;
			nbhds = [(-1,-1), (-1,0), (-1,+1), (0,-1), (0,+1), (+1,-1), (+1,0), (+1,+1)];
			countneighbors(x, y) := sum( apply(nbhds, delta, get(delta.x + x, delta.y + y)));

			newstate(x, y) := (
				number = countneighbors(x, y); //number of living neighbors
				if(get(x, y) == 1,
					if((number < 2) % (number > 3), 0, 1)
				, // else
					if(number == 3, 1, 0)
				)
			);
			ray(t) := origin + t * dir;
			raytrace()  := (
				a = 0;
				stepsize = zoom * EXTENT * 1.73205080757 / 512;
				repeat(512,
					a = a + voxelAt(ray(# * stepsize));
				);
				1 - (re(sqrt(a)) / 16)
			);
		</script>
		<script id="csmousedrag" type="text/x-cindyscript">
			d = mouse()-lastmouse;
			lambda = lambda-d.x;
			phi = phi-d.y;
			lastmouse = mouse();
		</script>
		<script id="csmousedown" type="text/x-cindyscript">
			lastmouse = mouse();
		</script>
		<script type="text/javascript">
			CindyJS({
				scripts: "cs*",
				autoplay: true,
				ports: [{
					id: "CSCanvas",
					width: 800,
					height: 800,
					transform: [{
						visibleRect: [-1, 1, 1, -1]
					}]
				}]
			});
		</script>
	</body>
</html>
