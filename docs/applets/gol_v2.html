<!DOCTYPE html>
<html>
  <head>
    <script type="text/javascript" src="https://cindyjs.org/dist/snapshot/Cindy.js"></script>
    <script type="text/javascript" src="https://cindyjs.org/dist/snapshot/CindyGL.js"></script>
    <title>Conway's Game of Life in CindyGL</title>
  </head>

  <body>
    <div id="CSCanvas"></div>

    <script id="csinit" type="text/x-cindyscript">
      extent = 128;
      createimage("gol", extent, extent);
      colorplot("gol", if(random() > .6, 1, 0)); //random values as starting image

      get(x, y) := imagergb([0, 0], [extent, 0], "gol", [x, y], interpolate -> false, repeat -> true).r;

      deltas = [(-1,-1), (-1,0), (-1,+1), (0,-1),  (0,+1), (+1,-1), (+1,0), (+1,+1)];
      countneighbors(x, y) := sum( apply(deltas, delta, get(delta.x + x, delta.y + y)));

      newstate(x, y) := (
        number = countneighbors(x, y); //number of living neighbors
        if(get(x, y) == 1,

          //if the cell lives then it will die if it has less than 2 neighbours or more than 3 neighbours
          if((number < 2) % (number > 3), 0, 1),

          //if cell was dead then 3 are required to be born
          if(number == 3, 1, 0)
        )
      );

      tiles = ceil(sqrt(extent));
      EXTENT = tiles*extent;
      createimage("history", EXTENT, EXTENT);
      canvas([0, 0], [EXTENT, 0], "history", drawimage([0, 0], [extent, 0], "gol", interpolate -> false));
      t = 0;

      target = .5*extent*[1, 1, 1];
      r = 1.2*extent;
      lambda = 0;
      phi = 0;

      ray(t) := origin + t*dir;

      stepsize = (r + .866*extent)/500;
      raytracing() := (
        volume = 0;
        repeat(500, volume = volume + evaluateVoxel(ray(#*stepsize)));
        re(log(volume + 1))/log(300);
        //re(sqrt(volume))/sqrt(300);
      );

      evaluateVoxel(voxel) := if((min(voxel) < 0) % (extent < max(voxel)), 0,
        tile = mod(ceil(voxel.y) - extent + t, extent);
        position = [voxel.x + extent*mod(tile, tiles), voxel.z + extent*floor(tile/tiles)];
        imagergb([0, 0], [EXTENT, 0], "history", position, interpolate -> false).r;
      );
    </script>

    <script id="csdraw" type="text/x-cindyscript">
      colorplot([0, 0], [extent, 0], "gol", newstate(#.x, #.y)); //overwrite texture "gol"
      t = mod(t + 1, extent);
      x = extent*mod(t, tiles);
      y = extent*floor(t/tiles);
      canvas([0, 0], [EXTENT, 0], "history", drawimage([x, y], [x + extent, y], "gol", interpolate -> false));
      //drawimage([-1, -1], [1, -1], "history", interpolate -> false);

      origin = r*[cos(lambda)*cos(phi), sin(phi), sin(lambda)*cos(phi)] + target;
      mdir = (target - origin)/|(target - origin)|;
      v = cross([0, 1, 0], mdir);
      w = cross(mdir, v);
      v = v/|v|;
      w = w/|w|;

      colorplot(
        dir = mdir + #.x*v + #.y*w;
        dir = dir/|dir|;
        raytracing();
      );
    </script>

    <script id="csmousedown" type="text/x-cindyscript">
      lastmouse = mouse();
    </script>

    <script id="csmousedrag" type="text/x-cindyscript">
      d = mouse()-lastmouse;
      lambda = lambda-d.x;
      phi = phi-d.y;
      lastmouse = mouse();
    </script>

    <script type="text/javascript">
    CindyJS({
      scripts: "cs*",
      autoplay: true,
      ports: [{
        id: "CSCanvas",
        width: 512,
        height: 512,
        transform: [{
          visibleRect: [-1, 1, 1, -1]
        }]
      }]
    });
    </script>
  </body>
</html>
